plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'io.github.mrsaraira'
version = '2.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    compileOnly "org.projectlombok:lombok:$LIB_LOMBOK_VERSION"
    annotationProcessor "org.projectlombok:lombok:$LIB_LOMBOK_VERSION"

    testCompileOnly "org.projectlombok:lombok:$LIB_LOMBOK_VERSION"
    testAnnotationProcessor "org.projectlombok:lombok:$LIB_LOMBOK_VERSION"
    testImplementation platform("org.junit:junit-bom:$LIB_JUNIT_BOM")
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

signing {
    required { gradle.taskGraph.hasTask("generateMetadataFileForMavenPublication") }
    sign publishing.publications
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'io.github.mrsaraira'
            artifactId = 'constant-containers'

            from components.java

            pom {
                name = 'constant-containers'
                description = 'Flexible generic data structure for storing immutable constant values and supports complex relations'
                url = 'https://github.com/mrsaraira/constant-containers.git'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://raw.githubusercontent.com/mrsaraira/constant-containers/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        id = 'tsaraira'
                        name = 'Takhsin Saraira'
                        email = 'mrsarayra@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/mrsaraira/constant-containers.git'
                    developerConnection = 'scm:git:ssh://github.com/mrsaraira/constant-containers.git'
                    url = 'https://github.com/mrsaraira'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'release'
            url = 'https://s01.oss.sonatype.org/content/repositories/releases'

            credentials {
                username mavenUsername
                password mavenPassword
            }
        }
        maven {
            name = 'snapshot'
            url = 'https://s01.oss.sonatype.org/content/repositories/snapshots'

            credentials {
                username mavenUsername
                password mavenPassword
            }
        }
    }
}
